apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nexus'
apply plugin: 'java2html'

group = 'org.gradle.api.plugins'
version = '0.3'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-java2html-plugin:0.3'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    java2html 'de.java2html:java2html:5.0'
}

java2html {
    conversion {
        tabs = 4
        style = 'eclipse'
        outputFormat = 'html'
        showLineNumbers = true
        addLineAnchors = true
        showFileName = true
        useShortFileName = true
        overwrite = true
        showDefaultTitle = true
        includeDocumentHeader = true
        includeDocumentFooter = true
    }

    overview {
        docDescription = 'Gradle plugin for turning source code into HTML, RTF, TeX and XHTML using Java2HTML.'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Java2HTML plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.3'
}

modifyPom {
    project {
        name 'Gradle Java2HTML plugin'
        description 'Gradle plugin for turning source code into HTML, RTF, TeX and XHTML using Java2HTML.'
        url 'https://github.com/bmuschko/gradle-java2html-plugin'
        inceptionYear '2012'

        scm {
            url 'https://github.com/bmuschko/gradle-java2html-plugin'
            connection 'scm:https://bmuschko@github.com/bmuschko/gradle-java2html-plugin.git'
            developerConnection 'scm:git://github.com/bmuschko/gradle-java2html-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bmuschko'
                name 'Benjamin Muschko'
                email 'benjamin.muschko@gmail.com'
            }
        }
    }
}
