plugins {
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.jfrog.bintray'              version '1.8.4'
    id 'net.nemerosa.versioning'        version '2.7.1'
    id 'com.github.ben-manes.versions'  version '0.20.0'
    id 'com.gradle.plugin-publish'      version '0.10.0'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.bmuschko'
version = '2.0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Java2HTML plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

idea.project {
    jdkName = '1.7'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom {
                name = 'Gradle Java2HTML plugin'
                description = 'Gradle plugin for turning source code into HTML, RTF, TeX and XHTML using Java2HTML.'
                url = 'https://github.com/bmuschko/gradle-java2html-plugin'
                inceptionYear = '2012'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/bmuschko/gradle-java2html-plugin'
                    connection = 'scm:https://bmuschko@github.com/bmuschko/gradle-java2html-plugin.git'
                    developerConnection = 'scm:git://github.com/bmuschko/gradle-java2html-plugin.git'
                }
                developers {
                    developer {
                        id = 'bmuschko'
                        name = 'Benjamin Muschko'
                        email = 'benjamin.muschko@gmail.com'
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
    key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'com.bmuschko:gradle-java2html-plugin'
        desc = 'Gradle plugin for turning source code into HTML, RTF, TeX and XHTML using Java2HTML.'
        websiteUrl = "https://github.com/bmuschko/${project.name}"
        issueTrackerUrl = "https://github.com/bmuschko/${project.name}/issues"
        vcsUrl = "https://github.com/bmuschko/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'source', 'code', 'html', 'rtf', 'tex', 'xhtml', 'transform', 'documentation']
        publicDownloadNumbers = true

        version {
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': "com.bmuschko.java2html:${project.group}:${project.name}"]

            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? project.getProperty('gpgPassphrase') : null
            }
        }
    }
}

gradlePlugin {
    plugins {
        jdepsPlugin {
            id = 'com.bmuschko.java2html'
            implementationClass = 'com.bmuschko.gradle.java2html.Java2HTMLPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/bmuschko/gradle-java2html-plugin'
    vcsUrl = 'https://github.com/bmuschko/gradle-java2html-plugin'
    description = 'Gradle plugin for turning source code into HTML, RTF, TeX and XHTML using Java2HTML.'
    tags = ['gradle', 'source', 'code', 'html', 'rtf', 'tex', 'xhtml', 'transform', 'documentation']

    plugins {
        jdepsPlugin {
            id = 'com.bmuschko.java2html'
            displayName = 'Gradle Java2HTML plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}
